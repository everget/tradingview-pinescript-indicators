//@version=6
// Copyright (c) 2021-present, Alex Orekhov (everget)
// Nick Rypock Trailing Reverse (NRTR) script may be freely distributed under the terms of the GPL-3.0 license.
indicator('Nick Rypock Trailing Reverse', shorttitle = 'NRTR', overlay = true)

const string calcGroup = 'Calculation'
percentage = input.float(2, minval = 0, maxval = 100, step = 0.1, title = 'Coefficient of Correction, %', group = calcGroup) / 100

const string visualGroup = 'Visuals'
showLabels = input.bool(true, title = 'Show Buy/Sell Labels', group = visualGroup)
highlightState = input.bool(true, title = 'Highlight State', group = visualGroup)

//---

var int trend = 0
var float hp = close
var float lp = close
nrtr = close

if trend >= 0
    if close > hp
        hp := close
        hp
    nrtr := hp * (1 - percentage)
    if close <= nrtr
        trend := -1
        lp := close
        nrtr := lp * (1 + percentage)
        nrtr
else
    if close < lp
        lp := close
        lp
    nrtr := lp * (1 + percentage)
    if close > nrtr
        trend := 1
        hp := close
        nrtr := hp * (1 - percentage)
        nrtr

const color textColor = color.white
const color longColor = color.green
const color shortColor = color.red
const color longFillColor = color.new(color.green, 85)
const color shortFillColor = color.new(color.red, 85)

longStopPlot = plot(trend == 1 ? nrtr : na, title = 'Long Stop', style = plot.style_linebr, linewidth = 2, color = longColor)
buySignal = trend == 1 and trend[1] == -1
plotshape(buySignal ? nrtr : na, title = 'Long Stop Start', location = location.absolute, style = shape.circle, size = size.tiny, color = longColor)
plotshape(buySignal and showLabels ? nrtr : na, title = 'Buy Label', text = 'Buy', location = location.absolute, style = shape.labelup, size = size.tiny, color = longColor, textcolor = textColor)

shortStopPlot = plot(trend == 1 ? na : nrtr, title = 'Short Stop', style = plot.style_linebr, linewidth = 2, color = shortColor)
sellSignal = trend == -1 and trend[1] == 1
plotshape(sellSignal ? nrtr : na, title = 'Short Stop Start', location = location.absolute, style = shape.circle, size = size.tiny, color = shortColor)
plotshape(sellSignal and showLabels ? nrtr : na, title = 'Sell Label', text = 'Sell', location = location.absolute, style = shape.labeldown, size = size.tiny, color = shortColor, textcolor = textColor)

midPricePlot = plot(ohlc4, title = '', display = display.none, editable = false)

fill(midPricePlot, longStopPlot, title = 'Long State Filling', color = (highlightState and trend == 1 ? longFillColor : na))
fill(midPricePlot, shortStopPlot, title = 'Short State Filling', color = (highlightState and trend == -1 ? shortFillColor : na))

alertcondition(trend != trend[1], title = 'NRTR Direction Change', message = 'NRTR has changed direction, {{exchange}}:{{ticker}}')
alertcondition(buySignal, title = 'NRTR Buy', message = 'NRTR Buy, {{exchange}}:{{ticker}}')
alertcondition(sellSignal, title = 'NRTR Sell', message = 'NRTR Sell, {{exchange}}:{{ticker}}')
