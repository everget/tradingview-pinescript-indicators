//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Trend Intensity Index script may be freely distributed under the MIT license.
study("Trend Intensity Index", shorttitle="TII")

majorLength = input(title="Major Length", type=integer, minval=1, defval=60)
minorLength = input(title="Minor Length", type=integer, minval=1, defval=30)
upperLevel = input(title="Upper Level", type=integer, defval=80)
lowerLevel = input(title="Lower Level", type=integer, defval=20)
src = input(title="Source", type=source, defval=close)

sma = sma(src, majorLength)

positiveSum = 0.0
negativeSum = 0.0

for i = 0 to minorLength - 1
	positiveSum := positiveSum + (nz(src[i]) > nz(sma[i]) ? nz(src[i]) - nz(sma[i]) : 0.0)
	negativeSum := negativeSum + (nz(src[i]) > nz(sma[i]) ? 0.0 : nz(sma[i]) - nz(src[i]))

tii = 100 * positiveSum / (positiveSum + negativeSum)

upperBand = hline(title="Upper Band", price=upperLevel, linestyle=dotted, color=gray)
middleBand = hline(title="Middle Band", price=50, linestyle=dashed, color=gray)
lowerBand = hline(title="Lower Band", price=lowerLevel, linestyle=dotted, color=gray)

fill(title="Background", hline1=upperBand, hline2=lowerBand, color=color(#cc0000, 94))

tiiColor = tii > upperLevel ? #0ebb23 : tii < lowerLevel ? #ff0000 : #3c78d8
plot(tii, title="TII", linewidth=2, color=tiiColor, transp=0)
