//@version=3
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Range Action Verification Index (RAVI) script may be freely distributed under the MIT license.
study("Range Action Verification Index (RAVI)", shorttitle="RAVI")

fastLength = input(title="Fast MA Length", type=integer, minval=1, defval=7)
fastMAInput = input(title="Fast MA", defval="SMA", options=["EMA", "SMA", "VWMA", "WMA"])
slowLength = input(title="Slow MA Length", type=integer, minval=1, defval=65)
slowMAInput = input(title="Slow MA", defval="SMA", options=["EMA", "SMA", "VWMA", "WMA"])
comparisonLookback = input(title="Comparison Lookback", type=integer, minval=1, defval=1)
src = input(title="Source", type=source, defval=close)

fastMA = iff(fastMAInput == "EMA", ema(src, fastLength),
	 iff(fastMAInput == "SMA", sma(src, fastLength),
	 iff(fastMAInput == "VWMA", vwma(src, fastLength),
	 iff(fastMAInput == "WMA", wma(src, fastLength),
	 na))))

slowMA = iff(slowMAInput == "EMA", ema(src, slowLength),
	 iff(slowMAInput == "SMA", sma(src, slowLength),
	 iff(slowMAInput == "VWMA", vwma(src, slowLength),
	 iff(slowMAInput == "WMA", wma(src, slowLength),
	 na))))

ravi = abs(100 * (fastMA - slowMA) / slowMA)

raviColor = ravi > nz(ravi[comparisonLookback]) ? green : red

plot(ravi, title="RAVI", linewidth=2, color=raviColor, transp=0)
